# Extra configure tests to support XeTeX

AC_DEFUN([AC_HAS_CARBON], [
#
#   Check for Carbon framework on Mac OS X
#
  AC_MSG_CHECKING([for Apple Carbon framework])
  AC_TRY_COMPILE(
    [
#include <Carbon/Carbon.h>
      ], [
ATSUStyle style;
    ],
    [ AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE_CARBON, 1, [Define if you have Carbon])
      LDCARBON="-framework Carbon"
      has_carbon=yes
    ],
    [ AC_MSG_RESULT(no)
      LDCARBON=
      has_carbon=no
    ]
  )
])

AC_DEFUN([AC_HAS_QUICKTIME], [
#
#   Check for QuickTime framework on Mac OS X
#
  AC_MSG_CHECKING([for Apple QuickTime framework])
  AC_TRY_COMPILE(
    [
#include <QuickTime/QuickTime.h>
      ], [
ImageDescriptionHandle h;
    ],
    [ AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE_QUICKTIME, 1, [Define if you have QuickTime])
      LDQUICKTIME="-framework QuickTime"
      has_quicktime=yes
    ],
    [ AC_MSG_RESULT(no)
      LDQUICKTIME=
      has_quicktime=no
    ]
  )
])

AC_DEFUN([AC_HAS_LIBFONTCONFIG], [
#
#   Check for libfontconfig
#
  _cppflags=$CPPFLAGS _ldflags=$LDFLAGS
  AC_ARG_WITH(fontconfig,
    [  --with-fontconfig=DIR   use fontconfig include/library files from DIR],
    [
if test -d "$withval"; then
  CPPFLAGS="$CPPFLAGS -I$withval/include"
  LDFLAGS="$LDFLAGS -L$withval/lib"
  FONTCONFIGCPPFLAGS="-I$withval/include"
  FONTCONFIGLDFLAGS="-L$withval/lib"
fi
    ]
  )
  AC_MSG_CHECKING([for fontconfig header files])
  AC_TRY_COMPILE(
    [
#include <fontconfig/fontconfig.h>
      ], [
FcObjectSet *os;
    ],
    [ AC_MSG_RESULT(yes)
      AC_CHECK_LIB(fontconfig, FcInit, [
        AC_DEFINE(HAVE_LIBFONTCONFIG, 1, [Define if you have libfontconfig])
        LDFONTCONFIG="-lfontconfig"
      ])
    ],
    [ AC_MSG_RESULT(no)
      CPPFLAGS=$_cppflags LDFLAGS=$_ldflags
      LDFONTCONFIG=
    ]
  )
])
