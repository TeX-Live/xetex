if test "$needs_freetype2" = no; then
  FREETYPE2CPPFLAGS=
  LDFREETYPE2=
  FREETYPE2DEP=
else
  EXTRA_FREETYPE2_INCLUDE=
  EXTRA_FREETYPE2_LIBDIR=

  if test "$with_system_freetype2" = yes; then
    if test "x$with_freetype2_libdir" != x; then
      EXTRA_FREETYPE2_LIBDIR="-L$with_freetype2_libdir"
    fi
    if test "x$with_freetype2_include" != x; then
      EXTRA_FREETYPE2_INCLUDE="-I$with_freetype2_include"
    fi
  fi

  OLD_LDFLAGS=$LDFLAGS
  OLD_CPP=$CPP
  OLD_LIBS=$LIBS

  LDFLAGS="$LDFLAGS $EXTRA_FREETYPE2_LIBDIR"
  CPP="$CPP $EXTRA_FREETYPE2_INCLUDE"

  if test "$with_system_freetype2" = yes; then
    AC_CHECK_LIB(freetype, FT_Init_FreeType,,
      AC_MSG_WARN(Standard FreeType2 library not found. Compiling my own.)
      with_system_freetype2=no)
  fi

  if test "$with_system_freetype2" = yes; then
    AC_CHECK_HEADERS(ft2build.h)
    if test "$ac_cv_header_ft2build_h" != yes; then
      AC_MSG_WARN(ft2build.h not found. Using my own freetype2 sources.)
      with_system_freetype2=no
    fi
  fi

  LDFLAGS=$OLD_LDFLAGS
  CPP=$OLD_CPP
  LIBS=$OLD_LIBS

  if test "$with_system_freetype2" != yes; then
    FREETYPE2CPPFLAGS='-I$(FREETYPE2SRCDIR)/include'
    LDFREETYPE2='$(FREETYPE2DIR)/.libs/libfreetype.a'
    FREETYPE2DEP='$(FREETYPE2DIR)/.libs/libfreetype.a'
    using_system_freetype2=no
  else
    FREETYPE2CPPFLAGS="$EXTRA_FREETYPE2_INCLUDE"
    LDFREETYPE2="$EXTRA_FREETYPE2_LIBDIR -lfreetype"
    FREETYPE2DEP=
    using_system_freetype2=yes
  fi
fi

AC_SUBST(FREETYPE2CPPFLAGS)
AC_SUBST(LDFREETYPE2)
AC_SUBST(FREETYPE2DEP)
