dnl
dnl ChkTeX v1.5, configuration file.
dnl Copyright (C) 1995-96 Jens T. Berger Thielemann
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl
dnl Contact the author at:
dnl             Jens Berger
dnl             Spektrumvn. 4
dnl             N-0666 Oslo
dnl             Norway
dnl             E-mail: <jensthi@ifi.uio.no>
dnl
dnl


dnl Process this file with autoconf to produce a configure script.
AC_INIT(ChkTeX.c)
AC_SUBST(SCRIPTS)
SCRIPTS=
AC_SUBST(ELISPDIR)
ELISPDIR=


AC_ARG_ENABLE(lacheck-replace,[
    --enable-lacheck-replace:  Installs  a lacheck replacement for the
                               AUC-TeX interface.  (Default:  no).],,
enable_lacheck_replace=no)

AC_ARG_ENABLE(emacs-hack,[
    --enable-emacs-hack:       Installs a Elisp file, which makes ChkTeX
                               available in Emacs. Must be followed by
                               the path of your elisp files (e.g.
                               "--enable-emacs-hack=/usr/lib/emacs/").
                               (Default: no).],,
enable_emacs_hack=no)

AC_ARG_ENABLE(debug-info,[
    --enable-debug-info:       Compile in miscellaneous runtime debugging
			       information.  (Default: yes).],,
enable_debug_info=yes)

if test "$silent" != "yes"; then
cat <<EOF


             #####  ###       ###    ########     ###     ##
           ###   ##  ##        ##       ##          ##   #0
          @@         @@ @@@    @@  @@   @@  #######  @@ @
          @@         @@@  @@   @@ @@    @@   ##   #   @@
          &&         &&   &&   &&&&     &&   @@      & &&
           &&&   &&  &&   &&   && &&    &&   @@@@   &   &&
             %%%%%  %%%   %%% %%%  %%%  %%   &&   %%%    %%%
                                             &&   &
                                            %%%%%%%

               Welcome to the ChkTeX configuration script!
            Before taking the program in use, please read the
             COPYING file; it applies to this script as well
            as the remaining parts of the package.  In short:

                        *NO WARRANTIES WHATSOEVER*

                           Copyright © 1995-96
                        Jens T. Berger Thielemann
                          <jensthi@ifi.uio.no>
EOF
sleep 1
cat <<EOF
Supported features:
    --enable-lacheck-replace:  Installs  a lacheck replacement for the
                               AUC-TeX interface.  (Default:  no).
    --enable-debug-info:       Compile in miscellaneous runtime debugging
			       information.  (Default: yes).
    --enable-emacs-hack:       Installs a Elisp file, which makes ChkTeX
                               available in Emacs. Must be followed by
                               the path of your elisp files (e.g.
                               "--enable-emacs-hack=/usr/lib/emacs/").
                               (Default: no).
EOF
sleep 1
fi

AC_MSG_CHECKING(whether lacheck replacement should be installed)
if test "$enable_lacheck_replace" = "yes"; then
  SCRIPTS="$SCRIPTS lacheck"
  AC_MSG_RESULT(yes)
else
  SCRIPTS=
  AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING(whether runtime debug info should be used)
if test "$enable_debug_info" = "yes"; then
  AC_MSG_RESULT(yes)
else
  AC_DEFINE(STRIP_DEBUG, 1)
  AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING(whether the emacs hack should be installed)
case $enable_emacs_hack in
  no) AC_MSG_RESULT(no)
      ;;
  /*|*/*) ELISPDIR=$enable_emacs_hack
     AC_MSG_RESULT(yes)
    ;;
  *) echo;
    AC_MSG_ERROR(--enable-emacs-hack needs the *full path* of Elisp files)
     ;;
esac

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROGS(PERL5, perl5 perl, no, $PATH:/usr/bin:/usr/local/bin)
if test "$PERL5" != no; then
  AC_MSG_CHECKING(whether perl is v5 or higher)
  if $PERL5 -e 'exit($] >= 5)'; then
    PERL5=no
    AC_MSG_RESULT(no)
  else
    AC_MSG_RESULT(yes)
  fi
fi

if test "$PERL5" != no; then
    AC_SYS_INTERPRETER
    if test "$ac_cv_sys_interpreter" = "yes"; then
	SCRIPTS="$SCRIPTS chkweb deweb"
    else
	AC_MSG_WARN(deweb will not be installed since your system does)
	AC_MSG_WARN(not support the pound-bang notation.)
    fi
else
    AC_MSG_WARN(deweb will not be installed since you do not have perl5.)
fi

AC_CHECK_PROGS(GROFF, 'groff -man -P-bu -Tlatin1' 'nroff -man', 'nroff -man')

AC_PATH_PROG(LATEX, latex)
AC_PATH_PROG(DVIPS, dvips)

AC_PATH_PROG(LYNX, lynx)
AC_PATH_PROG(LATEX2HTML, latex2html)

dnl Checks for libraries.
AC_CHECK_LIB(termlib, tgetent)
AC_CHECK_LIB(termcap, tgetent)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CONFIG_HEADER(config.h)
AC_CHECK_HEADERS(limits.h stat.h strings.h stdarg.h dnl
sys/stat.h termcap.h termlib.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(access closedir fileno isatty opendir readdir stat dnl
strcasecmp strdup strlwr strtol)

AC_OUTPUT(Makefile deweb MakeHTML)
