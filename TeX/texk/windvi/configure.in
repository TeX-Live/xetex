dnl Process this file with autoconf to produce a configure script.
AC_INIT(xdvi.c)

AC_CONFIG_HEADER(c-auto.h:c-auto.in)
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_LN_S

sinclude(../kpathsea/withenable.ac)

AC_FUNC_VFORK
AC_FUNC_VPRINTF
AC_FUNC_MEMCMP
AC_HEADER_SYS_WAIT
AC_TYPE_SIGNAL
AC_C_BIGENDIAN
# Put here by Thomas Esser for teTeX.  This causes some tests to be run
# twice by the kps version.
AC_PATH_XTRA
AC_TYPE_SIZE_T

# XDVI checks by Paul Vojta
XDVI_C_BITMAP_TYPE
XDVI_SYS_STREAMS
XDVI_SYS_SUNOS_4
XDVI_FUNC_POLL

# These were probably missing from the test-set?
# On the other hand, they are not used already, why start now? -janl
# AC_TYPE_OFF_T
# AC_HEADER_TIME

# <sys/bsdtypes.h> is for ISC 4.0, to define fd_set in psgs.c.
AC_CHECK_FUNCS(memcpy strerror waitpid sigaction strchr tempnam setenv)
AC_CHECK_HEADERS(netdb.h sys/bsdtypes.h X11/Xosdefs.h)

# For the bitmap size. -- I think this is in XDVI_C_BITMAP_TYPE now -janl
# AC_CHECK_SIZEOF(long)

sinclude(../kpathsea/xt.ac)
sinclude(withenable.ac)
sinclude(../../libs/libwww/withenable.ac)
sinclude(../../libs/libwww/libwww.ac)
sinclude(../../libs/zlib/withenable.ac)
sinclude(../../libs/zlib/zlib.ac)

# For --with-x-toolkit.  Only distinguish `no' from everything else.
# They don't get to disable X entirely, this is *x*dvi.

# -lXext may not be present, and that may be ok.  Link with it only if it is.
if test -z "$x_ext_lib"; then # allow envvar override
  if test -n "$x_libraries"; then
    XLFLAG="-L$x_libraries"
  else
    XLFLAG=
  fi
  AC_CHECK_LIB(Xext, XextCreateExtension,
               x_ext_lib=-lXext, :, -lX11 $X_EXTRA_LIBS $XLFLAG)
fi
AC_SUBST(x_ext_lib)

if test "x$with_x_toolkit" = xno; then
  AC_DEFINE(NOTOOL)
  x_tool_libs=
else
  # Effectively promote --with-x-toolkit=xt to athena since the code
  # does not distinguish.
  x_tool_libs="-lXaw -lXmu -lXt"
fi
AC_SUBST(x_tool_libs)

# For --with-ps.
test -z "$PS_DEF" && PS_DEF=-DPS_GS
test "$PS_DEF" = no && PS_DEF=
AC_SUBST(PS_DEF)dnl
dnl
# If we're on Solaris and we want DPS, then add the extra compilation stuff
# to get it.
if echo "$PS_DEF" | grep PS_DPS >/dev/null; then
   if test "`(uname) 2>/dev/null`" = SunOS \
      && uname -r | grep '^5' >/dev/null; then
    AC_MSG_RESULT(adding DPS includes and libraries for Solaris)
    X_CFLAGS="$X_CFLAGS -I/usr/openwin/include/X11"
    AC_CHECK_LIB(sunmath, isinf, ac_sunmath=-lsunmath, ac_sunmath="")
    X_LIBS="$X_LIBS -R/usr/openwin/lib /usr/openwin/lib/libdps.so $ac_sunmath"
    # We only need isinf and ieeefp.h to work around a Solaris bug in
    # -ldps; see psdps.c.  isinf is in -lsunmath.
    AC_CHECK_FUNCS(isinf)
    AC_CHECK_HEADERS(ieeefp.h)
  else
    # SunOS 4.x is simpler.
    AC_MSG_RESULT(adding DPS includes and libraries for SunOS 4.x)
    X_LIBS="$X_LIBS -ldps"
  fi
elif echo "$PS_DEF" | grep PS_NEWS >/dev/null; then
    AC_MSG_RESULT(adding NeWS libraries)
    X_LIBS="$X_LIBS -lxview -lcps -lolgx -lxvps"
fi

AC_OUTPUT(Makefile)

dnl update `stamp-auto' since we just remade `c-auto.h'
date >stamp-auto
